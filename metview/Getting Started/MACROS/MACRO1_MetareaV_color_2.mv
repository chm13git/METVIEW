# Metview Macro

#  **************************** LICENSE START ***********************************
# 
#  Copyright 2018 ECMWF. This software is distributed under the terms
#  of the Apache License version 2.0. In applying this license, ECMWF does not
#  waive the privileges and immunities granted to it by virtue of its status as
#  an Intergovernmental Organization or submit itself to any jurisdiction.
# 
#  ***************************** LICENSE END ************************************

# ---------------------------------------------------------------
# Description: Plot METAREA_V
# ---------------------------------------------------------------

# Device description
out = pdf_output(output_name : "metarea_v")

# Checks running mode
mode = runmode()
if mode = 'batch' or mode = 'execute' then
   setoutput(out)
end if

# Annotation definition
annotation = mtext(
    text_line_count     : 10,
    text_line_1         : "A - ALFA Arroio Chui - Cabo de Santa Marta",
    text_line_2         : "B - BRAVO Cabo de Santa Marta - Cabo Frio (Oceanic)",
    text_line_3         : "C - CHARLIE Cabo de Santa Marta - Cabo Frio (Coastal)",
    text_line_4         : "D - DELTA Cabo Frio - Caravelas",
    text_line_5         : "E - ECHO Caravelas - Salvador",
    text_line_6         : "F - FOXTROT Salvador - Natal",
    text_line_7         : "G - GOLF Natal - Sao Luiz",
    text_line_8         : "H - HOTEL Sao Luiz - Cabo Orange",
    text_line_9         : "N - NOVEMBER North Oceanic Area",
    text_line_10        : "S - SIERRA South Oceanic Area",
    text_colour         : "black",
    text_font_size      : 0.3,
    text_justification  : "left",
    text_mode           : "positional",
    text_box_x_position : 4, #5.4,
    text_box_y_position : 8, #10.2,
    text_box_x_length   : 6,
    text_box_y_length   : 3
    )
    
# Title definition
title = mtext(
    text_line_1    : "METAREA V",
    text_font_size : 0.5
    )

# Cities definition
cities = mvl_cities()

# Areas definition
#areas = draw_area_A()  # only one area
areas = mvl_study_areas()
areas_vd = mgraph(
    graph_line_colour    : "black",
    graph_line_thickness : 2
    )

# The big study area definition
the_area = mvl_the_study_area()
the_area_vd = mgraph(
   graph_type           : "area",
   graph_line_style     : "dash",
   graph_line_colour    : "red",
   graph_line_thickness : 1,
   graph_shade_colour   : "RGBA(0.9922, 0.9569, 0.7294, 0.8)"
   )

# Study areas labels definition
labels = mvl_study_areas_labels()

# Land/sea shade definitions
land_shade = mcoast(
      map_coastline_resolution        : "medium",
      map_coastline_land_shade        : "on",
      map_coastline_land_shade_colour : "RGB(0.8824,0.6784,0.5922)",
      map_grid_colour                 : "grey",
      map_grid_line_style             : "dash",
      map_label_height                : 0.2,
      map_label_top                   : "off"
     )

sea_shade = mcoast(
      map_coastline_resolution        : "medium",
      map_coastline_sea_shade         : "on",
      map_coastline_sea_shade_colour  : "RGB(0.8039,0.9137,0.9725)",
      map_grid                        : "off",
      map_label                       : "off"
     )

# Call function to build layout (defined at end of macro)
dw = build_layout()

# Plotting
plot(dw[1], sea_shade, the_area, the_area_vd, land_shade)
plot(dw[2], title, areas, areas_vd, cities, labels, annotation)

#------------------------------------------

# Function to build the layout
function build_layout()

   # Coastlines definition
   no_coast = mcoast(
      map_coastline         : "off",
      map_boundaries        : "on",
      map_boundaries_colour : "RGB(0.9,0.9,0.9)",
      map_grid              : "off",
      map_label             : "off"
     )

   # Geographical view description
   gview1 = geoview(
      map_projection      : "cylindrical",
      map_area_definition : "corners",
      area                : [-40, -75, 10, -5]
     )

   gview2 = geoview(
      map_projection      : "cylindrical",
      map_area_definition : "corners",
      area                : [-40, -75, 10, -5],
      coastlines          : no_coast
     )

   # Page description
   page1 = plot_page(view : gview1)
   page2 = plot_page(view : gview2)
   
   # SuperPage description
   dw = plot_superpage(
      layout_size   : "CUSTOM",
      custom_width  : 25,
      custom_height : 18,
      pages         : [page1,page2]
     )

   return dw

end build_layout

include FUNCAO_mvl_study_areas_2
include FUNCAO_mvl_cities_2