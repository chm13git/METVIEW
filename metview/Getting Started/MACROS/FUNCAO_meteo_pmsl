#Metview Macro

# Metview Macro

#  **************************** LICENSE START ***********************************
# 
#  Copyright 2018 ECMWF. This software is distributed under the terms
#  of the Apache License version 2.0. In applying this license, ECMWF does not
#  waive the privileges and immunities granted to it by virtue of its status as
#  an Intergovernmental Organization or submit itself to any jurisdiction.
# 
#  ***************************** LICENSE END ************************************

function meteo_pmsl( lat, lon, pmsl )

   # Get time series values
   pmsl_vals = nearest_gridpoint(pmsl, lat, lon)  #interpolate()

   # Get time series valid dates
   x_vals = valid_date(pmsl)

   # Define a customised min/max values for y axis, around 10% more 
   ymin = minvalue(vector(pmsl_vals))
   ymax = maxvalue(vector(pmsl_vals))
   delta = int(((ymax - ymin) * 0.10) + 1)
   ymin = max(ymin-delta,0)
   ymax = int(ymax + delta)

   # Define visualisation data structure and its visual definition
   ts = input_visualiser(
           input_x_type        : "date",
           input_y_values      : pmsl_vals,
           input_date_x_values : x_vals
          )

   ts_vd = mgraph(
              graph_line_colour    : "greenish_blue",
              graph_line_thickness : 4
             )

   # Title definition
   title = mtext(
       text_line_1        : "Pressao ao nivel medio do mar (hPa)",
       text_colour        : "black",
       text_justification : "left",
       text_font_size     : 0.5
      )

   # Return icon definitions
   # Return a list containing 3 elements where the last element is
   # list containing the instructions for the plot command
   return [ymin, ymax, [title, ts, ts_vd]]

end meteo_pmsl